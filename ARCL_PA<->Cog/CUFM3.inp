TITLE:
Curve of Factors with strong measurement invariance and some error covariances
DATA:
FILE =
C:\Users\PWS5\OneDrive - University of Pittsburgh\
Desktop\SoN_Proj\Yurun\LGCMplusGraph.txt ;

VARIABLE:
NAMES = 
IDNo PA1 PA2 PA3 PA4 PA5 
CVLtca1 CVLtca2 CVLtca3 CVLtca4 CVLtca5 
CVLfrl1 CVLfrl2 CVLfrl3 CVLfrl4 CVLfrl5 
TRA1 TRA2 TRA3 TRA4 TRA5  
TRB1 TRB2 TRB3 TRB4 TRB5  
DIGB1 DIGB2 DIGB3 DIGB4 DIGB5 
DIGF1 DIGF2 DIGF3 DIGF4 DIGF5 
DSS1 DSS2 DSS3 DSS4 DSS5 
BOS1 BOS2 BOS3 BOS4 BOS5 
FLUC1 FLUC2 FLUC3 FLUC4 FLUC5 
FLUL1 FLUL2 FLUL3 FLUL4 FLUL5
CLK111 CLK112 CLK113 CLK114 CLK115 
CLK31 CLK32 CLK33 CLK34 CLK35 
CRD1 CRD2 CRD3 CRD4 CRD5
AGE1 AGE2 AGE3 AGE4 AGE5;
 
USEVARIABLES= 

CVLtca1 CVLtca2 CVLtca3 
CVLfrl1 CVLfrl2 CVLfrl3 

TRA1 TRA2 TRA3
TRB1 TRB2 TRB3 
DIGB1 DIGB2 DIGB3 
DIGF1 DIGF2 DIGF3 
DSS1 DSS2 DSS3 

BOS1 BOS2 BOS3 
FLUC1 FLUC2 FLUC3 
FLUL1 FLUL2 FLUL3

CLK111 CLK112 CLK113 
CLK31 CLK32 CLK33
CRD1 CRD2 CRD3;

MISSING=ALL(-9999);

ANALYSIS: 
TYPE=GENERAL;
ESTIMATOR=BAYES;
ALGORITHM=MH;
STARTS=20;
STVALUES=ML;
FBITER=20000;
THIN=10;
BSEED=39412;
PROCESSORS=8;
MODEL=NOCOVARIANCES;

MODEL:
!constrain loadings;
G1 BY
CVLtca1 (l1)
CVLfrl1 (l2)
TRA1 (l3)
TRB1 (l4)
DIGB1 (l5)
DIGF1 (l6)
DSS1 (l7)
BOS1 (l8)
FLUC1 (l9)
FLUL1 (l10)
CLK111 (l11) 
CLK31 (l12)
CRD1 (l13) 
;

G2 BY
CVLtca2 (l1)
CVLfrl2 (l2)
TRA2 (l3)
TRB2 (l4)
DIGB2 (l5)
DIGF2 (l6)
DSS2 (l7)
BOS2 (l8)
FLUC2 (l9)
FLUL2 (l10)
CLK112 (l11)
CLK32 (l12)
CRD2 (l13)
;


G3 BY
CVLtca3 (l1)
CVLfrl3 (l2)
TRA3 (l3)
TRB3 (l4)
DIGB3 (l5)
DIGF3 (l6)
DSS3 (l7)
BOS3 (l8)
FLUC3 (l9)
FLUL3 (l10)
CLK113 (l11)
CLK33 (l12)
CRD3 (l13)
;




!constrain intercepts;
[CVLtca1 CVLtca2 CVLtca3] (a1);
[CVLfrl1 CVLfrl2 CVLfrl3] (a2);
[TRA1 TRA2 TRA3] (a3);
[TRB1 TRB2 TRB3] (a4);
[DIGB1 DIGB2 DIGB3] (a5);
[DIGF1 DIGF2 DIGF3] (a6);
[DSS1 DSS2 DSS3] (a7);
[BOS1 BOS2 BOS3] (a8);
[FLUC1 FLUC2 FLUC3] (a9);
[FLUL1 FLUL2 FLUL3] (a10);
[CLK111 CLK112 CLK113] (a11);
[CLK31 CLK32 CLK33] (a12);
[CRD1 CRD2 CRD3] (a13);

!set time specific factor intercepts to 0;
[G1-G3@0];

!curve of factors model;
I S|G1@0 G2@1 G3@2;

!covariances for curve factors;
I WITH S;

!error covariances from baseline set at each time and constrained across time;
CVLfrl1-CVLfrl3 PWITH CVLtca1-CVLtca3 (c1);
TRA1-TRA3 PWITH TRB1-TRB3 (c2);
DIGF1-DIGF3 PWITH DIGB1-DIGB3 (c3);
FLUL1-FLUL3 PWITH FLUC1-FLUC3 (c5);
CLK111-CLK113 PWITH CLK31-CLK33 (c6);

OUTPUT:
STANDARDIZED
TECH1 TECH4;

PLOT:
TYPE=PLOT2;

!savedata: 
!FILE= LGCM_G_confInv.dat;
!SAVE=FSCORES(20);
!FACTORS=G1 G2 G3 G4 G5 I S;
