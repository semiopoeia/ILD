TITLE:
Curve of Factors with strong measurement invariance and some error covariances
DATA:
FILE =
C:\Users\PWS5\OneDrive - University of Pittsburgh\
Desktop\SoN_Proj\Yurun\LGCMplus.txt ;

VARIABLE:
NAMES = 
IDNo PA1 PA2 PA3 PA4 PA5 PA6 
PA7 PA8 PA9 CVLtca1 CVLtca2 
CVLtca3 CVLtca4 CVLtca5 CVLtca6 
CVLtca7 CVLtca8 CVLtca9 CVLfrl1 
CVLfrl2 CVLfrl3 CVLfrl4 CVLfrl5 
CVLfrl6 CVLfrl7 CVLfrl8 CVLfrl9 
TRA1 TRA2 TRA3 TRA4 TRA5 TRA6 
TRA7 TRA8 TRA9 TRB1 TRB2 TRB3 
TRB4 TRB5 TRB6 TRB7 TRB8 TRB9 
DIGB1 DIGB2 DIGB3 DIGB4 DIGB5 
DIGB6 DIGB7 DIGB8 DIGB9 DIGF1 
DIGF2 DIGF3 DIGF4 DIGF5 DIGF6 
DIGF7 DIGF8 DIGF9 DSS1 DSS2 
DSS3 DSS4 DSS5 DSS6 DSS7 DSS8 
DSS9 BOS1 BOS2 BOS3 BOS4 BOS5 
BOS6 BOS7 BOS8 BOS9 FLUC1 FLUC2 
FLUC3 FLUC4 FLUC5 FLUC6 FLUC7 
FLUC8 FLUC9 FLUL1 FLUL2 FLUL3 
FLUL4 FLUL5 FLUL6 FLUL7 FLUL8 
FLUL9 CLK111 CLK112 CLK113 CLK114 
CLK115 CLK116 CLK117 CLK118 CLK119 
CLK31 CLK32 CLK33 CLK34 CLK35 CLK36 
CLK37 CLK38 CLK39 CRD1 CRD2 CRD3 
CRD4 CRD5 CRD6 CRD7 CRD8 CRD9 ACTI1 
ACTI2 ACTI3 ACTI4 ACTI5 ACTI6 ACTI7 
ACTI8 ACTI9 AGE1 AGE2 AGE3 AGE4 AGE5;
 
USEVARIABLES= 

CVLtca1 CVLtca2 CVLtca3 CVLtca4 CVLtca5
CVLfrl1 CVLfrl2 CVLfrl3 CVLfrl4 CVLfrl5

TRA1 TRA2 TRA3 TRA4 TRA5
TRB1 TRB2 TRB3 TRB4 TRB5
DIGB1 DIGB2 DIGB3 DIGB4 DIGB5
DIGF1 DIGF2 DIGF3 DIGF4 DIGF5
DSS1 DSS2 DSS3 DSS4 DSS5

BOS1 BOS2 BOS3 BOS4 BOS5
FLUC1 FLUC2 FLUC3 FLUC4 FLUC5
FLUL1 FLUL2 FLUL3 FLUL4 FLUL5

CLK111 CLK112 CLK113 CLK114 CLK115
CLK31 CLK32 CLK33 CLK34 CLK35
CRD1 CRD2 CRD3 CRD4 CRD5;

MISSING=ALL(-9999);

ANALYSIS: 
TYPE=GENERAL;
ESTIMATOR=BAYES;
ALGORITHM=MH;
STARTS=20;
STVALUES=ML;
FBITER=20000;
THIN=10;
BSEED=39412;
PROCESSORS=8;
MODEL=NOCOVARIANCES;

MODEL:
!constrain loadings;
G1 BY
CVLtca1*(1)
CVLfrl1*(2)
TRA1*(3)
TRB1*(4)
DIGB1*(5)
DIGF1*(6)
DSS1*(7)
BOS1*(8)
FLUC1*(9)
FLUL1*(10)
CLK111*(11)
CLK31*(12)
CRD1*(13) 
;
G1@1;

G2 BY
CVLtca2*(1)
CVLfrl2*(2)
TRA2*(3)
TRB2*(4)
DIGB2*(5)
DIGF2*(6)
DSS2*(7)
BOS2*(8)
FLUC2*(9)
FLUL2*(10)
CLK112*(11)
CLK32*(12)
CRD2*(13)
;
G2@1;

G3 BY
CVLtca3*(1)
CVLfrl3*(2)
TRA3*(3)
TRB3*(4)
DIGB3*(5)
DIGF3*(6)
DSS3*(7)
BOS3*(8)
FLUC3*(9)
FLUL3*(10)
CLK113*(11)
CLK33*(12)
CRD3*(13)
;
G3@1;

G4 BY 
CVLtca4*(1)
CVLfrl4*(2)
TRA4*(3)
TRB4*(4)
DIGB4*(5)
DIGF4*(6)
DSS4*(7)
BOS4*(8)
FLUC4*(9)
FLUL4*(10)
CLK114*(11)
CLK34*(12)
CRD4*(13)
;
G4@1;

G5 BY
CVLtca5*(1)
CVLfrl5*(2)
TRA5*(3)
TRB5*(4)
DIGB5*(5)
DIGF5*(6)
DSS5*(7)
BOS5*(8)
FLUC5*(9)
FLUL5*(10)
CLK115*(11)
CLK35*(12)
CRD5*(13)
;
G5@1;

!constrain intercepts;
[CVLtca1 CVLtca2 CVLtca3 CVLtca4 CVLtca5](14);
[CVLfrl1 CVLfrl2 CVLfrl3 CVLfrl4 CVLfrl5](15);
[TRA1 TRA2 TRA3 TRA4 TRA5](16);
[TRB1 TRB2 TRB3 TRB4 TRB5](17);
[DIGB1 DIGB2 DIGB3 DIGB4 DIGB5](18);
[DIGF1 DIGF2 DIGF3 DIGF4 DIGF5](19);
[DSS1 DSS2 DSS3 DSS4 DSS5](20);
[BOS1 BOS2 BOS3 BOS4 BOS5](21);
[FLUC1 FLUC2 FLUC3 FLUC4 FLUC5](22);
[FLUL1 FLUL2 FLUL3 FLUL4 FLUL5](23);
[CLK111 CLK112 CLK113 CLK114 CLK115](24);
[CLK31 CLK32 CLK33 CLK34 CLK35](25);
[CRD1 CRD2 CRD3 CRD4 CRD5](26);

!set time specific factor intercepts to 0;
[G1-G5@0];

!curve of factors model;
I S|G1@0 G2@1 G3@2 G4@3 G5@4;

!covariances for curve factors;
I WITH S;

!error covariances from baseline set at each time and constrained across time;
CVLfrl1-CVLfrl5 PWITH CVLtca1-CVLtca5;
TRA1-TRA5 PWITH TRB1-TRB5;
DIGF1-DIGF5 PWITH DIGB1-DIGB5;
FLUL1-FLUL5 PWITH FLUC1-FLUC5;
CLK111-CLK115 PWITH CLK31-CLK35;

OUTPUT:
STANDARDIZED
TECH1 TECH4;

PLOT:
TYPE=PLOT2;